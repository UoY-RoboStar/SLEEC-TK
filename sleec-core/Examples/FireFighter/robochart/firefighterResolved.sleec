/********************************************************************************
 * Copyright (c) 2023 University of York and others
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Set of valid SLEEC rules for Firefighting UAV example. For technical reasons,
 * here we prefix their names with 'F', so as to be able to distinguish these
 * rules from those used in jss_running_example.sleec when specifying conformance
 * assertions in SLEEC-TK.
 */
 
 def_start
  event CameraStart
  measure personNearby:boolean
  event GoHome
  measure temperature:numeric
  event BatteryCritical
  event SprayWater
  measure detectPersonFallen:boolean
  event SeekHelp
  measure humanDetected:boolean
  measure animalDetected:boolean
  event RemainAnonymous
  event ReportLocation
  constant MAX_TEMPERATURE=30
  event FullyCharged
  event TestEfficiencyPreserved
  event StartMission
  event FinishMission
  constant MAX_MISSION_TIME=60
  event NotifyError
  measure authorized:boolean
  event OperatorInstruction
  event SoundWarningMessage
  measure distance:numeric
  event SoundAlarm
  measure batteryFull:boolean
  event BatteryAlarmandGoHome
def_end

rule_start
 
 FRule1 when SoundAlarm and batteryFull 
      then not GoHome within 5 minutes 
    
 // Relevant for conformance verification
 FRule2 when SoundAlarm then CameraStart within 1 minutes

 FRule3 when BatteryCritical then BatteryAlarmandGoHome within 5 minutes

 FRule4 when StartMission and detectPersonFallen
 then SeekHelp within 5 seconds

 FRule5 when CameraStart and humanDetected then RemainAnonymous
 FRule6 when CameraStart and animalDetected or humanDetected then ReportLocation
 
 // Relevant for conformance verification
 FRule7 when BatteryCritical then GoHome
	unless personNearby and temperature >  MAX_TEMPERATURE 
   	then SoundAlarm
 FRule8 when FullyCharged then TestEfficiencyPreserved within 24 hours

 FRule9 when StartMission then FinishMission within MAX_MISSION_TIME seconds
 	otherwise NotifyError within  1 hours
	unless not authorized

 FRule10 when OperatorInstruction then SoundWarningMessage within 1 seconds
 unless personNearby and distance<1 then not SprayWater within 1 seconds 

rule_end
