/********************************************************************************
 * Copyright (c) 2023 University of York and others
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * SPDX-License-Identifier: EPL-2.0
 *
 * Set of valid SLEEC rules for RAD example.
 */
 
def_start
event CurtainOpenRqt
event CurtainsOpened
event RefuseRequest
event InformUser
event DressingStarted
event CloseCurtains
measure userUnderDressed: boolean
measure medicalEmergency:boolean
measure userDistressed : scale(low, medium, high)
measure roomDark:boolean
measure notVisible:boolean
measure userAssent:boolean
measure consentGrantedwithinXmonths:boolean //X needs to be defined. In the language within is not allowed for measures.
event EmotionRecognitionDetected
measure competentIndicatorRequired: boolean
measure competentToGrantConsent:boolean
measure dressPreferenceTypeA:boolean
measure genderTypeB:boolean
event DressinginClotingX
measure userAdvices:boolean
measure clothingItemNotFound:boolean
event InteractionStarted
event InformUserThisIsAgentnotHuman
measure userConfused:boolean
measure buildingFloor:scale(f1,f2,f3,f4,f5,f6,f7,f8,f9,f10) //the building has max 10 floors.
measure theUserHasBeenInformed:boolean
event UserRequestInfo
event ProvideInfo
measure informationAvailable:boolean
event InformUserandandReferToHumanCarer
measure informationDisclosureNotPermitted:boolean
event ObtainAssent
measure admininisteringMedication:boolean
measure userCompetenceIndicator:numeric
constant N= 5 //this needs to be defined
event CheckForandObtainProxy
measure withholdingActivityPhysicalHarm:scale(wlow,wmoderate,wsevere)
measure consentGranted:boolean
measure consentIndicatorRequired:boolean
event StopActivity
measure consentIndicatorisWithdrawn:boolean
measure consentIndicatorisRevoked:boolean
event CollectionStarted
event StoreMinInfo
//second version
event DressingSuccessful
event FallAssessed
	measure userUndressed : boolean
	measure userUnresponsive : boolean
	event OpenCurtainsRequested
	event HealthChecked
	
//third version
	measure roomTemperature: numeric
	event DressingComplete
	constant MAX_RESPONSE_TIME = 60
	event UserFallen
	measure assentToSupportCalls:boolean
	event SupportCalled
	measure emergency : boolean
	event DressingAbandoned
	event RetryAgreed

def_end

rule_start

	// Empathetic, ethical. & IMPLICATION: promotes and supports user well-being 
	// Relevant for conformance verification [Rule 1 in paper].
	Rule12 when DressingStarted and userUnderDressed then DressingComplete  within 2 minutes  
		  	   unless roomTemperature < 19 then DressingComplete within 90 seconds
		       unless roomTemperature < 17 then DressingComplete within 60 seconds
	
	// Cultural, empathetic. & IMPLICATION: respect for privacy and cultural sensitivity
	// Relevant for conformance verification [Rule 2 in paper].
	Rule22 when CurtainOpenRqt then CurtainsOpened within 60 seconds
		       unless userUnderDressed then RefuseRequest within 30 seconds
		       unless userDistressed > medium then CurtainsOpened within MAX_RESPONSE_TIME seconds
   	
   	// Legal, ethical, social. & IMPLICATION: respect for autonomy and preventing harm
   	// Relevant for conformance verification [Rule 3 in paper].
   	Rule32 when UserFallen then SupportCalled within 1 minutes
   	           unless not assentToSupportCalls
   	           unless emergency
   	 
    // Legal, ethical. & IMPLICATION: promoting user beneficence and respecting autonomy
    // Relevant for conformance verification [Rule 4 in paper].
    Rule42 when DressingAbandoned then RetryAgreed within 3 minutes
		       otherwise {SupportCalled 
		  	      unless not assentToSupportCalls
		       }
		       
	// Irrelevant for conformance verification [Rule 5 in paper].
    Rule6 when UserRequestInfo then ProvideInfo
    		//unless not informationAvailable then InformUser and ReferToHumanCarer. The language does not allow the response with two events
			unless not informationAvailable then InformUserandandReferToHumanCarer
			unless informationDisclosureNotPermitted then InformUserandandReferToHumanCarer			   
	// Irrelevant for conformance verification [Rule 6 in paper].			   
	Rule4 when DressingStarted and dressPreferenceTypeA and genderTypeB then DressinginClotingX
			   unless userAdvices
			   unless medicalEmergency
			   unless clothingItemNotFound then InformUser // irrelevant		       

	// Relevant for conformance verification
	Rule52 when DressingStarted and roomTemperature >= 19 then DressingComplete  within 2 minutes

	/******************************************************************************
	 * Other rules below *
	 ******************************************************************************/
		       
	// Irrelevant for conformance verification
	Rule2 when DressingStarted then CloseCurtains //within 5 seconds //5 seconds can be specified for immediate action
			   unless medicalEmergency
			   unless buildingFloor >=f5
			   unless roomDark and notVisible
			   unless not userAssent
			   
	// Irrelevant for conformance verification
	Rule3 when EmotionRecognitionDetected and userDistressed>medium then InformUser 
		       unless consentGrantedwithinXmonths
			   unless not competentIndicatorRequired or not  competentToGrantConsent
	
			   
	// Irrelevant for conformance verification
	Rule5 	when InteractionStarted then InformUserThisIsAgentnotHuman
			unless medicalEmergency
			unless not userConfused
			//there is no temporal information in the language. at any time is representable.
			unless theUserHasBeenInformed // irrelevant

	
    
	// obtain consent/assent before dressing/administering medication
	// Irrelevant for conformance verification
	Rule7  when DressingStarted and admininisteringMedication then ObtainAssent
			unless userCompetenceIndicator = N then CheckForandObtainProxy
			unless medicalEmergency
			unless withholdingActivityPhysicalHarm >=wmoderate
			unless consentGranted
			unless not consentIndicatorRequired
			unless consentIndicatorisWithdrawn or consentIndicatorisRevoked then StopActivity

	// Irrelevant for conformance verification
	Rule8 	when CollectionStarted then StoreMinInfo

 	// Irrelevant for conformance verification
  	Rule13 when DressingStarted then DressingSuccessful
  
	// Irrelevant for conformance verification
	Rule23 when FallAssessed and userUnresponsive then SupportCalled
	
	// Irrelevant for conformance verification
	Rule63 when UserFallen then HealthChecked within 30 seconds
		otherwise SupportCalled within MAX_RESPONSE_TIME seconds
		
	// Irrelevant for conformance verification
	Rule73 when OpenCurtainsRequested then CurtainsOpened within 60 seconds
	
rule_end




